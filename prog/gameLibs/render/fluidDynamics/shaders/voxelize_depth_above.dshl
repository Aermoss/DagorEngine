include "shader_global.dshl"
include "depth_above.dshl"
include "cfd_inc.dshl"

int4 cfd_voxel_tex_size;
float4 cfd_voxel_size;
float4 cfd_world_box_min;

shader voxelize_depth_above
{
  ENABLE_ASSERT(cs)
  INIT_DEPTH_ABOVE(cs, blurred_depth)
  USE_DEPTH_ABOVE_TC(cs)

  (cs) {
    depth_above@smpArray = depth_around;
    voxel_tex@uav = cfd_voxel_tex hlsl {
      RWTexture3D<float> voxel_tex@uav;
    };
    tex_size@i3 = (cfd_voxel_tex_size.x, cfd_voxel_tex_size.y, cfd_voxel_tex_size.z);
    voxel_size@f3 = cfd_voxel_size;
    world_box_min@f3 = cfd_world_box_min;
  }

  hlsl(cs) {
    float3 get_voxel_corner_in_world(uint3 id)
    {
      return world_box_min + id.xzy * voxel_size;
    }
  }

  hlsl(cs) {
    [numthreads(8, 8, 1)]
    void cs_main(uint3 tid : SV_DispatchThreadID)
    {
      if (tid.x >= tex_size.x || tid.y >= tex_size.y || tid.z >= tex_size.z)
        return;

      float3 voxelCorner = get_voxel_corner_in_world(tid);
      float3 voxelCenter = voxelCorner + voxel_size * 0.5f;
      float2 depthAboveTc = saturate(world_to_depth_ao_extra.xy * voxelCenter.xz + world_to_depth_ao_extra.zw) - depth_ao_heights_extra.zw;

      // tc.z is 1 because we use the bigger cascade always
      float height = decode_depth_above(tex3Dlod(depth_above, float4(depthAboveTc, 1, 0)).x);

      if (height > voxelCenter.y)
        voxel_tex[tid] = 1;
      else
        voxel_tex[tid] = 0;
    }
  }
  compile("cs_5_0", "cs_main")
}